#!/usr/bin/env nu
#
# Will edit a priviliged file without privilige.
#
# Dependencies:
# - doas (duh)
#
# AN3223 © 2022
# João Farias © 2022-2024 BeyondMagic <beyondmagic@mail.ru>

use std log

let LIMIT_TRIES = 3

def clean [temppath: string] -> null {
	rm -rf $temppath
}

def read_file [filepath: string] -> string {
	mut content = ''
	if not (do { test -r $filepath } | complete | get exit_code | into bool) {
		$content = (cat $filepath)
	} else {
		$content = (doas cat $filepath)
	}
	return $content
}

# Like sudoedit, but for doas.
#
# Every argument will be treated as a file to edit. There's no suppot for passing arguments to doas, so you can only doas root.
#
# This script is SECURIRTY SENSTIVE! Special care has been taken to correctly preserve file attributes. Please exercise CAUTION when modifyin AND using this script.
def main [
	...filepaths: string # Filepath to edit, either absolute or relative.
] -> int {
	if ($filepaths | is-empty) {
		log error " Given no files to edit. Specify which files."
		exit 1
	}

	# Exit if environment variable EDITOR is not set.
	if $env.EDITOR == "" {
		log error " Environment variable EDITOR is not set, please set it."
		exit 1
	}

	for $filepath in $filepaths {

		let fullpath = (readlink -f $filepath)

		# Test if the file exists AND it is a file.
		if (do { test -f $fullpath } | complete | get exit_code) != 0 {
			log error $" '($filepath)' is not a file or does not exist."
			exit 1
		}

		# Exit if we can write the file.
		if (do { test -w $fullpath } | complete | get exit_code) == 0 {
			log error $" '($filepath)' is a file you can edit!"
			exit 1
		}

		let tmp = (basename $fullpath | prepend '/tmp/' | str join)
		cd /tmp/

		try {
			read_file $fullpath | save $tmp --force
			run-external $env.EDITOR $tmp

			if (do { diff --ignore-trailing-space --brief $fullpath $tmp } | complete | get exit_code) == 0 {

				log warning " File unchanged, exiting..."

			} else {

				mut times = 0
				while $times < $LIMIT_TRIES {
					$times += 1

					let exit_code = (do { doas dd ($tmp | prepend "if=" | str join) ($fullpath | prepend "of=" | str join) } | complete | get exit_code)

					# Wrong password attempt.
					if $exit_code == 1 {
						
						if $times == $LIMIT_TRIES {
							log warning $" You failed ($LIMIT_TRIES) times, the limit, exiting..."
							break
						}
						continue

					# Forced exit.
					} else if $exit_code == -1 {
						log warning " Canceled, exiting..."
						break

					# Successful attemp.
					} else {
						log debug " Done, changes written."
						break
					}
				}

			}
		} catch {
			log debug " Caught something, exiting."
		}
		clean $tmp
		cd -
	}
}
